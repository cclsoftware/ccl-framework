def isDebug = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().contains("Debug") }
def isRelease = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().contains("Release") }

def isClean = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().equals("clean") }

project.ext {
    buildConfiguration = "sync"
}

if (isDebug || isRelease) {
    def isArm7 = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().contains("Arm7") }
    def isArm8 = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().contains("Arm8") }

    def isX86_64 = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().contains("X86_64") }
    def isX86 = gradle.startParameter.taskRequests.any { !it.args.isEmpty() && it.args.first().contains("X86") && !it.args.first().contains("X86_64") }

    project.ext {
        buildConfiguration = isDebug ? "Debug" : "Release"
        buildArchitecture = isArm7 ? "ARM" : (isArm8 ? "ARM64" : (isX86_64 ? "x64" : (isX86 ? "x86" : "unknown")))
        buildABI = isArm7 ? "armeabi-v7a" : (isArm8 ? "arm64-v8a" : (isX86_64 ? "x86_64" : (isX86 ? "x86" : "unknown")))

        cmakeConfiguration = isDebug ? "Debug" : "RelWithDebInfo"

        if (buildArchitecture == "unknown") {
            throw new GradleException('error: unable to identify architecture')
        }
    }
}

if (isClean) {
    project.ext {
        buildConfiguration = "clean"
    }
}
