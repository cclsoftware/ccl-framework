set (REPOSITORY_MACROS_FILE ${CMAKE_CURRENT_LIST_FILE})

# Generate a list of relative paths in json array syntax
# @param {STRING} in  Name of a CMake list variable containing absolute paths
# @param {STRING} out  Name of the output variable
macro (ccl_generate_path_list in out)
	set (${out} "[ ")
	set (first ON)

	set (normalized_dirs "")
	foreach (dir ${${in}})
		cmake_path (IS_PREFIX REPOSITORY_ROOT "${dir}" NORMALIZE is_prefix)
		if (is_prefix)
			cmake_path (RELATIVE_PATH dir BASE_DIRECTORY "${REPOSITORY_ROOT}" OUTPUT_VARIABLE relative_dir)
		else ()
			set (relative_dir "${dir}")
		endif ()
		cmake_path (NORMAL_PATH relative_dir OUTPUT_VARIABLE normalized_dir)
		list (APPEND normalized_dirs "${normalized_dir}")
	endforeach ()
	
	list (REMOVE_DUPLICATES normalized_dirs)

	foreach (dir ${normalized_dirs})
		if (NOT first)
			string (APPEND ${out} ", ")
		endif ()

		set (first OFF)
		string (APPEND ${out} "\"${dir}\"")
	endforeach ()

	string (APPEND ${out} " ]")
endmacro ()

# Generate a json file containing information about the repository layout
# @param {FILEPATH} file  Path to the generated file.
macro (ccl_generate_repo_info file)
	if (NOT REPOSITORY_ROOT)
		set (repo_file_path "${file}")
		cmake_path (GET repo_file_path PARENT_PATH REPOSITORY_ROOT)
	endif ()

	ccl_generate_path_list (VENDOR_SUBMODULE_DIRS submodule_dirs)
	ccl_generate_path_list (VENDOR_SKIN_DIRS skin_dirs)
	ccl_generate_path_list (VENDOR_IDENTITY_DIRS identity_dirs)
	ccl_generate_path_list (VENDOR_SIGNING_DIRS signing_dirs)
	ccl_generate_path_list (VENDOR_TEMPLATE_DIRS template_dirs)
	ccl_generate_path_list (VENDOR_DOCUMENTATION_DIRS documentation_dirs)
	ccl_generate_path_list (VENDOR_CLASSMODEL_DIRS classmodel_dirs)
	ccl_generate_path_list (VENDOR_TOOL_DIRS tool_dirs)
	ccl_generate_path_list (VENDOR_TRANSLATIONS_DIRS translations_dirs)

	cmake_path (IS_PREFIX REPOSITORY_ROOT "${REPOSITORY_MACROS_FILE}" NORMALIZE is_prefix)
	if (is_prefix)
		cmake_path (RELATIVE_PATH REPOSITORY_MACROS_FILE BASE_DIRECTORY "${REPOSITORY_ROOT}" OUTPUT_VARIABLE repo_macros_file)
	else ()
		set (repo_macros_file "${REPOSITORY_MACROS_FILE}")
	endif ()
	
	cmake_path (IS_PREFIX REPOSITORY_ROOT "${CMAKE_CURRENT_LIST_FILE}" NORMALIZE is_prefix)
	if (is_prefix)
		cmake_path (RELATIVE_PATH CMAKE_CURRENT_LIST_FILE BASE_DIRECTORY "${REPOSITORY_ROOT}" OUTPUT_VARIABLE callsite)
	else ()
		set (callsite "${CMAKE_CURRENT_LIST_FILE}")
	endif ()
		
	file (GENERATE OUTPUT "${file}" CONTENT
"{
	\"comment\": 
	[
		\"generated by ccl_generate_repo_info\",
		\"defined in ${repo_macros_file}\",
		\"called from ${callsite}\"
	],
	\"submodules\": ${submodule_dirs},
	\"skins\": ${skin_dirs},
	\"identities\": ${identity_dirs},
	\"signing\": ${signing_dirs},
	\"templates\": ${template_dirs},
	\"documentation\": ${documentation_dirs},
	\"classmodels\": ${classmodel_dirs},
	\"tools\": ${tool_dirs},
	\"translations\": ${translations_dirs}
}
"
		NEWLINE_STYLE UNIX)
endmacro ()
