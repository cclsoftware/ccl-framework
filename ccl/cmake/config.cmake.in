@PACKAGE_INIT@

set (ccl_LIBRARIES "")

if ("${VENDOR_PLATFORM}" STREQUAL "")
	if (WIN32)
		set (VENDOR_PLATFORM_DETECTED win)
	elseif (APPLE)
		set (VENDOR_PLATFORM_DETECTED mac)
	elseif (UNIX)
		set (VENDOR_PLATFORM_DETECTED linux)
	endif ()
endif ()

set (VENDOR_PLATFORM ${VENDOR_PLATFORM_DETECTED} CACHE STRING "Target platform, e.g. win or zephyr.")

if (PACKAGE_PREFIX_DIR STREQUAL "/")
  set (PACKAGE_PREFIX_DIR "")
endif ()

if (WIN32)
	set (CCL_TOOLS_BINDIR "${PACKAGE_PREFIX_DIR}/@CCL_BINARY_DESTINATION@")
	set (CCL_DIR "${PACKAGE_PREFIX_DIR}/@CCL_PUBLIC_HEADERS_DESTINATION@")
	set (CCL_CMAKE_DIR "${PACKAGE_PREFIX_DIR}/cmake")
	set (CCL_SKINS_DIR "${PACKAGE_PREFIX_DIR}/skins")
	set (CCL_TEMPLATES_DIR "${PACKAGE_PREFIX_DIR}/templates")
	set (CCL_CLASSMODELS_DIR "${PACKAGE_PREFIX_DIR}/classmodels")
	list (APPEND CCL_NSIS_DEFINITIONS "NSIS_INCLUDES_DIR=${CCL_DIR}/packaging/win/nsis")
elseif (ANDROID)
	set (CCL_FRAMEWORK_ROOT "${PACKAGE_PREFIX_DIR}")
	set (CCL_TOOLS_BINDIR "${PACKAGE_PREFIX_DIR}/@CCL_BINARY_DESTINATION@")
	set (CCL_DIR "${PACKAGE_PREFIX_DIR}/@CCL_PUBLIC_HEADERS_DESTINATION@")
	set (CCL_CMAKE_DIR "${PACKAGE_PREFIX_DIR}/@CCL_CMAKE_EXPORT_DESTINATION@")

	get_filename_component (CCL_LIBRARY_DIR "${PACKAGE_PREFIX_DIR}/@CCL_STATIC_LIBRARY_DESTINATION@" PATH)

	set (CCL_BUILD_SUPPORT_DIR "${PACKAGE_PREFIX_DIR}/@CCL_SUPPORT_DESTINATION@/build")
	set (CCL_SKINS_DIR "${PACKAGE_PREFIX_DIR}/@CCL_SUPPORT_DESTINATION@/skins")
	set (CCL_TEMPLATES_DIR "${PACKAGE_PREFIX_DIR}/@CCL_SUPPORT_DESTINATION@/templates")
	set (CCL_CLASSMODELS_DIR "${PACKAGE_PREFIX_DIR}/@CCL_SUPPORT_DESTINATION@/classmodels")

	get_filename_component (CCL_ANDROID_NDK_SYSROOT "${CMAKE_CXX_COMPILER}" PATH)
	get_filename_component (CCL_ANDROID_NDK_SYSROOT "${CCL_ANDROID_NDK_SYSROOT}" PATH)
	set (CCL_ANDROID_NDK_SYSROOT "${CCL_ANDROID_NDK_SYSROOT}/sysroot")
elseif (APPLE)
	set (CCL_TOOLS_BINDIR "${PACKAGE_PREFIX_DIR}/@CCL_BINARY_DESTINATION@")
	set (CCL_DIR "${PACKAGE_PREFIX_DIR}/@CCL_PUBLIC_HEADERS_DESTINATION@")
	set (CCL_CMAKE_DIR "${PACKAGE_PREFIX_DIR}/@CCL_CMAKE_EXPORT_DESTINATION@")
	set (CCL_SKINS_DIR "${PACKAGE_PREFIX_DIR}/skins")
	set (CCL_TEMPLATES_DIR "${PACKAGE_PREFIX_DIR}/templates")
	set (CCL_CLASSMODELS_DIR "${PACKAGE_PREFIX_DIR}/classmodels")
else ()
	set (CCL_TOOLS_BINDIR "/@CCL_BINARY_DESTINATION@")
	set (CCL_DIR "/@CCL_PUBLIC_HEADERS_DESTINATION@")
	set (CCL_CMAKE_DIR "/@CCL_CMAKE_EXPORT_DESTINATION@")
	set (CCL_SKINS_DIR "/@CCL_SUPPORT_DESTINATION@/skins")
	set (CCL_TEMPLATES_DIR "@CCL_SUPPORT_DESTINATION@/templates")
	set (CCL_CLASSMODELS_DIR "@CCL_SUPPORT_DESTINATION@/classmodels")
endif ()

list (APPEND CMAKE_PREFIX_PATH "${CCL_CMAKE_DIR}")

if ("${VENDOR_TARGET_ARCHITECTURE}" STREQUAL "")
	include (${CCL_CMAKE_DIR}/targetarch.cmake)
	target_architecture (VENDOR_TARGET_ARCHITECTURE_DETECTED)
	set (VENDOR_TARGET_ARCHITECTURE "${VENDOR_TARGET_ARCHITECTURE_DETECTED}")
endif ()

if (APPLE)
	string (REPLACE ";" "-" ARCH_NAME "${VENDOR_TARGET_ARCHITECTURE}") # for Apple universal binaries (mac-x86_64;arm64 is ONE configuration)
else ()
	set (ARCH_NAME "${VENDOR_TARGET_ARCHITECTURE}")
endif ()
include ("${CMAKE_CURRENT_LIST_DIR}/${VENDOR_PLATFORM}-${ARCH_NAME}/ccl-targets.cmake")

include (${CCL_CMAKE_DIR}/coremacros.cmake)
include (${CCL_CMAKE_DIR}/cclmacros.cmake)
include (${CCL_CMAKE_DIR}/repomacros.cmake)

foreach (component ${ccl_FIND_COMPONENTS})
	if (TARGET ${component})
		list (APPEND ccl_LIBRARIES ${component})
		ccl_include_platform_specifics (${component})
		if ("${component}" MATCHES "ccltext|cclsystem|cclsecurity|cclnet|cclgui")
			ccl_copy_imported_target (${component} FOLDER ccl)
		endif ()
		if ("${component}" MATCHES "ccltext|cclsystem|cclsecurity|cclnet|cclgui|corelib")
			target_include_directories (${component} AFTER INTERFACE "${CCL_DIR}/resource")
		endif ()
		get_target_property (component_interface_sources "${component}" INTERFACE_SOURCES)
		source_group ("source/ccl" FILES ${component_interface_sources})
		set (ccl_${component}_FOUND TRUE)
	endif ()
endforeach ()
  		
check_required_components (ccl)

list (APPEND VENDOR_INCLUDE_DIRS "${CCL_DIR}/resource")
list (APPEND VENDOR_SKIN_DIRS "${CCL_SKINS_DIR}")
list (APPEND VENDOR_TEMPLATE_DIRS "${CCL_TEMPLATES_DIR}")
list (APPEND VENDOR_CLASSMODEL_DIRS "${CCL_CLASSMODELS_DIR}")
list (APPEND VENDOR_TOOL_DIRS "${CCL_TOOLS_DIR}")

set (CCL_IDENTITIES_DIR "${CCL_CMAKE_DIR}/identities")
list (APPEND VENDOR_IDENTITY_DIRS "${CCL_IDENTITIES_DIR}")
